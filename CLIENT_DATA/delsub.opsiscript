Message "Uninstalling " + $ProductName$ + " ..."

Set $SearchPattern$ = $ProductName$
; Parameter: $SearchPattern$ Suchbegriff in Registry
Sub_search_registry3264_uninstall_keys
; Rückgabewert: $ResultList$ gefundene Einträge

Switch count ($ResultList$)
	Case "0"
		comment "No installations of " + $SearchPattern$ + " found in $RegPathUninstall$. Nothing to do ;-)"
	EndCase
	Case "1"
		Set $RegId$ = takeSTring(0, $ResultList$)
		Set $UninstallProgram$ = GetRegistryStringValueSysNative("[" + $RegPathUninstall$ + "\" + $RegId$ + "] UninstallString")	
	EndCase
	DefaultCase
		logError "Found more than one installation! This case is not intended yet in this script! Stop now."
		isFatalError
	EndCase
EndSwitch

if not ( $UninstallProgram$ = "" )
    Winbatch_uninstall
    sub_check_exitcode

	if $DesktopLink$ = "true"
		comment "Delete common desktop link"
		Linkfolder_delete_desktoplink	
	endif

	comment "include custom post deinstall file"
	set $CustomPostDeinstall$ = getProductProperty("custom-post-deinstall","none")
	if not ($CustomPostDeinstall$ = "none")
		if FileExists("%ScriptPath%\custom\" + $CustomPostDeinstall$)
			include_insert "%ScriptPath%\custom\" + $CustomPostDeinstall$
		endif
	endif
endif

[Winbatch_uninstall]
$UninstallProgram$ /VERYSILENT

[Linkfolder_delete_desktoplink]
set_basefolder common_desktopdirectory
set_subfolder ""
delete_element $ProductName$

