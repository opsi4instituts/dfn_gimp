; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/credits/

[Actions]

; common Values for (un)installation
include_insert "common.opsiinc"

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError
	; Stop process and set installation status to failed
else
	comment "Show product picture"
	ShowBitmap "%ScriptPath%\" + $ProductId$ + ".png" $ProductName$
	
	if FileExists("%ScriptPath%\delsub.opsiscript")
		comment "Start uninstall sub section"
		Sub "%ScriptPath%\delsub.opsiscript"
	endif
	
	Message "Installing %installingProdName% (%installingProdVersion%)..."	

	comment "Start setup program"
	Winbatch_install
	Sub_check_exitcode

	Set $SearchPattern$ = $ProductName$
	; Parameter: $SearchPattern$ Suchbegriff in Registry
	Sub_search_registry64_uninstall_keys
	; Rückgabewert: $ResultList$ gefundene Einträge

	Switch count ($ResultList$)
		Case "0"
			logError "Fatal: After Installation " + $ProductName$ + " could not be found in " + $RegPathUninstall$
			isFatalError
		EndCase
		Case "1"
			comment "Successful Installation"
			comment "Get 'DisplayIcon' from registry for path to exe"
			Set $RegId$ = takeSTring(0, $ResultList$)
			Set $PathToExe$ = GetRegistryStringValue64("[" + $RegPathUninstall$ + "\" + $RegId$ + "] DisplayIcon")

			set $DesktopIcon$ = getProductProperty("desktopicon","no")
			if $DesktopIcon$ = "yes"
				comment "Create common desktop icon"
				Linkfolder_install_desktopicon	
			endif

			comment "include custom post install file"
			set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
			if not ($CustomPostInstall$ = "none")
				if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
					include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
				endif
			endif
		EndCase
	EndSwitch
endif

[Winbatch_install]
"%ScriptPath%\$InstallProgram$" /VERYSILENT /NORESTART

[Linkfolder_install_desktopicon]
set_basefolder common_desktopdirectory
set_subfolder ""

set_link
 	name: $ProductName$
 	target: $PathToExe$
end_link

