; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/credits/

[Actions]

; common Values for (un)installation
include_insert "common.opsiinc"

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError
	; Stop process and set installation status to failed
else
	if FileExists("%ScriptPath%\delsub.opsiscript")
		comment "Start uninstall sub section"
		Sub "%ScriptPath%\delsub.opsiscript"
	endif

	if ($MSVersion$ = "6.1")
		if ($INST_SystemType$ = "64 Bit System") and (($INST_architecture$ = "system specific") or ($INST_architecture$ = "both") or ($INST_architecture$ = "64 only"))
			Message "Installing " + $ProductName$ + " 64 Bit..."
			comment "Start setup program"
			Winbatch_install
			Sub_check_exitcode
			
			if (GetRegistryStringValue64("[" + $RegPath$ + "\" + $RegId$ + "] DisplayIcon") = "")
				logError "Fatal: After Installation " + $RegPath$ + "\" + $RegId$ + "] not found"
				isFatalError
			else
				comment "Successful Installation"
				comment "Get 'DisplayIcon' from registry for path to exe"
				Set $PathToExe$ = GetRegistryStringValue64("[" + $RegPath$ + "\" + $RegId$ + "] DisplayIcon")
				
				set $DesktopIcon$ = getProductProperty("desktopicon","no")
				if $DesktopIcon$ = "yes"
					comment "Create common desktop icon"
					Linkfolder_install_desktopicon	
				endif

				set $StartmenuEntry$ = getProductProperty("startmenuentry","no")
				if $StartmenuEntry$ = "no"
					comment "Create common startmenu entry"
					Linkfolder_install_startmenu	
				endif

				comment "include custom post install file"
				set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
				if not ($CustomPostInstall$ = "none")
					if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
						include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
					endif
				endif
			endif			
			
		else
			Logwarning "Only tested with 64 bit version!"
		endif
	else
		Logwarning "Only tested with windows 7!"
	endif
endif

[Winbatch_install]
"%ScriptPath%\$InstallProgram$" /VERYSILENT /NORESTART

[Linkfolder_install_desktopicon]
set_basefolder common_desktopdirectory
set_subfolder ""

set_link
 	name: $ProductName$
 	target: $PathToExe$
end_link

[Linkfolder_install_startmenu]
set_basefolder common_programs
delete_element $ProductName$

